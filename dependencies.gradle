//配置各个module共用的参数
apply plugin: 'kotlin-android'
// 使用@Parcelize注解必备
apply plugin: 'kotlin-parcelize'
//kotlin语言使用注入框架，dataBinding都需要这个，某些情况下已被ksp替代"
//apply plugin: 'kotlin-kapt'
// 替代kapt，但是要使用dataBinding仍要kapt
apply plugin: 'com.google.devtools.ksp'
//用于navigation传参的时候一种安全防护,非空，类型判断等。
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdk compile_sdk_version
//    buildToolsVersion build_tools_version

    defaultConfig {
        minSdk min_sdk_verion
        targetSdk target_sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 混淆配置
        proguardFiles 'proguard-sdk.pro', 'proguard-app.pro'
        // 解决应用程序方法数超过限制的问题，允许应用程序使用多个 dex 文件
        multiDexEnabled true

        // 动态向BuildConfig.java中写入值
        // 是否线上环境
        buildConfigField('boolean', 'IS_RELEASE_PACKAGE', '' + IS_RELEASE_PACKAGE + '')
        // 日志打印开关
        buildConfigField('boolean', 'LOG_ENABLE', '' + LOG_ENABLE + '')
        // log tag
        buildConfigField('String', 'LOG_TAG', '"' + 'CZF_TAG' + '"')
        // 服务器的主机地址
        buildConfigField('String', 'HOST_URL', '"' + HOST_URL + '"')
        // 编译环境
        buildConfigField('String', 'serverType', '"' + serverType + '"')
        // 版本名称
        buildConfigField('String', 'VERSIONNAME', '"' + VERSIONNAME + '"')
        // 版本号
        buildConfigField('int', 'VERSIONCODE', VERSIONCODE)
    }

    signingConfigs {
        debug {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }

        release {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    // Android Gradle使用总结: https://blog.51cto.com/zhaoyanjun/3815153
    /**
     * 编译环境
     * 编译环境配置后，会在Build Variants和Gradle（的other下）两个侧边栏中出现对应的环境
     */
    buildTypes {
        debug {
            // 调试模式开关
            jniDebuggable true
            // 压缩对齐开关
            zipAlignEnabled false
            // 移除无用的资源
//            shrinkResources false
            // 代码混淆开关
            minifyEnabled false
            // 签名信息配置
            signingConfig signingConfigs.debug
            // 调试模式下只保留一种架构的 so 库，提升打包速度
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        preview.initWith(debug)
        preview {
        }

        release {
            // 调试模式开关
            jniDebuggable false
            // 压缩对齐开关
            zipAlignEnabled true
            // 移除无用的资源
//            shrinkResources true
            // 代码混淆开关
            minifyEnabled true
            // 签名信息配置
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                // armeabi：万金油架构平台（占用率：0%）
                // armeabi-v7a：曾经主流的架构平台（占用率：10%）
                // arm64-v8a：目前主流架构平台（占用率：95%）
                abiFilters 'armeabi-v7a','arm64-v8a'
                // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
//            abiFilters 'arm64-v8a','x86_64' // 64位版本
            }
        }
    }

    buildFeatures {
        // dataBinding在KSP上不被支持：
        // 我们不打算支持 KSP，也不建议在此阶段使用数据绑定，因为 compose 是我们推荐的 UI 解决方案。
//        dataBinding = true
        viewBinding true
    }

    // 根据AS支持版本适配，但是直接用19也没问题，要用就用最高，jvmTarget也要同步
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    //使用jetPack的组件，会要求jdk 1.8最好，有的时候会遇到报错，需要配置如下，避免target jvm 1.8的那个报错
    kotlinOptions {
        jvmTarget = "19"
    }

    // 设置存放 so 文件的目录
    sourceSets {
        main {
            res.srcDirs(
                    'src/main/res',
                    // 使用头条屏幕适配方案替代此方案
//                    'src/main/res-sw'
            )
            jniLibs.srcDirs = ['libs']
        }
    }

    /**
     * 忽略警告
     */
    lint {
        // 表示 Lint 不会在出现错误时停止构建过程，而是继续构建其他的内容
        abortOnError false
        // 表示 Lint 不会检查代码中的所有警告，只检查 Android 官方文档中公认的警告
        checkAllWarnings false
        // 表示 Lint 不会对 Release 版本的 APK 进行检查，只对 Debug 版本进行检查
        checkReleaseBuilds false
        // 表示 Lint 忽略代码中的所有警告和建议，并且开始编译构建
        ignoreWarnings true
        // 表示 Lint 不会在控制台上输出任何消息或警告信息，只会在编译报告中显示
        quiet true
    }

    packagingOptions {
        jniLibs {
            //常见的引入的so库重复，pickFirst当有多个匹配项的时候匹配到第一个就可以了
            pickFirsts += ['lib/armeabi-v7a/libavdevice.so', 'lib/armeabi-v7a/libavcodec.so',
                           'lib/armeabi-v7a/libavfilter.so', 'lib/armeabi-v7a/libavformat.so',
                           'lib/armeabi-v7a/libavutil.so', 'lib/armeabi-v7a/libswresample.so',
                           'lib/armeabi-v7a/libswscale.so', 'lib/armeabi-v7a/libpostproc.so',
                           'lib/armeabi-v7a/libstlport_shared.so', 'lib/armeabi/libstlport_shared.so',
                           '**/libc++_shared.so']
            // 配置混淆工具ProGuard在优化 APK 时保留这些特定的库文件中的调试符号
            keepDebugSymbols += ['*/x86/*.so', '*/armeabi/*.so', '*/armeabi-v7a/*.so']
            // 配置混淆工具 ProGuard 在优化 APK 时排除掉所有的 META-INF 目录下的文件，以避免签名校验失败和应用程序无法安装等问题。
            excludes += ['META-INF/*******']
        }
        resources {
            // 配置混淆工具 ProGuard 在优化 APK 时排除掉所有的 META-INF 目录下的文件，以避免签名校验失败和应用程序无法安装等问题。
            excludes += ['META-INF/*******']
        }
    }
}

/**
 * 关闭所有警告
 */
afterEvaluate {
    tasks.withType(JavaCompile).tap {
        configureEach {
            // Try to turn them all off automatically
            options.compilerArgs << '-Xlint:none'
            options.compilerArgs << '-nowarn' // same as '-Xlint:none'

            // Turn them off manually
            options.compilerArgs << '-Xlint:-auxiliaryclass'
            options.compilerArgs << '-Xlint:-cast'
            options.compilerArgs << '-Xlint:-classfile'
            options.compilerArgs << '-Xlint:-deprecation'
            options.compilerArgs << '-Xlint:-dep-ann'
            options.compilerArgs << '-Xlint:-divzero'
            options.compilerArgs << '-Xlint:-empty'
            options.compilerArgs << '-Xlint:-fallthrough'
            options.compilerArgs << '-Xlint:-finally'
            options.compilerArgs << '-Xlint:-options'
            options.compilerArgs << '-Xlint:-overloads'
            options.compilerArgs << '-Xlint:-overrides'
            options.compilerArgs << '-Xlint:-path'
            options.compilerArgs << '-Xlint:-processing'
            options.compilerArgs << '-Xlint:-rawtypes'
            options.compilerArgs << '-Xlint:-serial'
            options.compilerArgs << '-Xlint:-static'
            options.compilerArgs << '-Xlint:-try'
            options.compilerArgs << '-Xlint:-unchecked'
            options.compilerArgs << '-Xlint:-varargs'
        }
    }
}

// 添加构建依赖项：https://developer.android.google.cn/studio/build/dependencies
// api 与 implementation 的区别：https://www.jianshu.com/p/8962d6ba936e
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // 平台相关的基础库
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.concurrent:concurrent-futures-ktx:1.1.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.fragment:fragment-ktx:1.5.7"
    implementation "androidx.activity:activity-ktx:1.7.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    //room
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    //注意配置navigation版本号的时候，project下的build.gradle的class path也需要同步navigation的版本号配置
    implementation "androidx.navigation:navigation-ui:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-runtime:$navigation_version"

    // gson
    implementation "com.google.code.gson:gson:2.10.1"

    //glide : https://github.com/bumptech/glide
    api "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:compiler:$glide_version"

    //okhttp3 :  https://github.com/square/okhttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    //retrofit  https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    //rxjava3 :  https://github.com/ReactiveX/RxJava
    implementation "io.reactivex.rxjava3:rxjava:3.1.6"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.2"

    // 第三方库

    //utils  https://github.com/Blankj/AndroidUtilCode
    // https://github.com/Blankj/AndroidUtilCode/blob/master/lib/utilcode/README-CN.md
    implementation "com.blankj:utilcodex:1.31.1"

    // https://github.com/JessYanCoding/RetrofitUrlManager
    implementation "me.jessyan:retrofit-url-manager:1.4.0"

    // BaseRecyclerViewAdapterHelper https://github.com/CymChad/BaseRecyclerViewAdapterHelper/wiki
    implementation "io.github.cymchad:BaseRecyclerViewAdapterHelper:4.0.0-beta10"

    // dslTablayout
    // 链接：https://github.com/angcyo/DslTabLayout
    // 文档：https://angcyo.gitee.io/doc/tablayout/
    implementation "com.github.angcyo.DslTablayout:TabLayout:$dslTab_version"
    //可选
    implementation "com.github.angcyo.DslTablayout:ViewPager1Delegate:$dslTab_version"
    //可选
    implementation "com.github.angcyo.DslTablayout:ViewPager2Delegate:$dslTab_version"

    //时间转换框架 https://github.com/dlew/joda-time-android
    implementation "net.danlew:android.joda:2.12.5"

    // banner https://github.com/youth5201314/banner
//    implementation "com.youth.banner:banner:2.2.2"

    // https://github.com/Tencent/MMKV
    implementation "com.tencent:mmkv:1.2.15"

    // 吐司框架：https://github.com/getActivity/Toaster
    implementation "com.github.getActivity:Toaster:12.2"

    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation "com.github.getActivity:XXPermissions:18.0"

    // Shape 框架：https://github.com/getActivity/ShapeView
    implementation "com.github.getActivity:ShapeView:8.5"

    // https://github.com/JessYanCoding/AndroidAutoSize
    implementation "com.github.JessYanCoding:AndroidAutoSize:v1.2.1"

    // https://github.com/gyf-dev/ImmersionBar
    implementation "com.geyifeng.immersionbar:immersionbar:3.2.2"

    // PictureSelector 基础 (必须) https://github.com/LuckSiege/PictureSelector
    implementation "io.github.lucksiege:pictureselector:$picture_version"
    // 图片压缩 (按需引入)
    implementation "io.github.lucksiege:compress:$picture_version"
    // 图片裁剪 (按需引入)
    implementation "io.github.lucksiege:ucrop:$picture_version"
    // 自定义相机 (按需引入)
    implementation "io.github.lucksiege:camerax:$picture_version"

    // core  https://github.com/lingochamp/okdownload
    implementation "com.liulishuo.okdownload:okdownload:$okdownload_version"
    // 提供sqlite来存储断点
    implementation "com.liulishuo.okdownload:sqlite:$okdownload_version"
    // 提供okhttp连接到后端,然后请自行导入okhttp依赖项
    implementation "com.liulishuo.okdownload:okhttp:$okdownload_version"
    // 提供与文件下载器匹配的接口
    implementation "com.liulishuo.okdownload:filedownloader:$okdownload_version"
    // 为kotlin扩展提供接口
    implementation "com.liulishuo.okdownload:ktx:$okdownload_version"

    // AOP 插件库：https://mvnrepository.com/artifact/org.aspectj/aspectjrt
    // AGP 7.1.3后已失效，保留引用是不想删代码
    implementation 'org.aspectj:aspectjrt:1.9.6'

    // 上拉刷新下拉加载框架： https://github.com/scwang90/SmartRefreshLayout
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'

    // https://docs.mapbox.com/android/maps/guides/install/
    implementation ('com.mapbox.maps:android:10.12.1'){
        // 排除mapbox自己引用的okhttp3
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }

    // 多渠道打包：https://github.com/Tencent/VasDolly
    implementation "com.tencent.vasdolly:helper:$vasdolly_version"

    // https://github.com/airbnb/lottie-android
    implementation 'com.airbnb.android:lottie:6.0.0'

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    // 多语种：https://github.com/getActivity/MultiLanguages
    // 悬浮窗：https://github.com/getActivity/XToast
    // 日志输出：https://github.com/getActivity/Logcat
    // 工具类：https://github.com/Blankj/AndroidUtilCode
    // 轮播图：https://github.com/bingoogolapple/BGABanner-Android
    // 二维码：https://github.com/bingoogolapple/BGAQRCode-Android
    // 跑马灯：https://github.com/sunfusheng/MarqueeView
    // 对象注解：https://www.jianshu.com/p/f1f888e4a35f
    // 对象存储：https://github.com/leavesC/DoKV
    // 多渠道打包：https://github.com/Meituan-Dianping/walle
    // 设备唯一标识：http://msa-alliance.cn/col.jsp?id=120
    // 嵌套滚动容器：https://github.com/donkingliang/ConsecutiveScroller
    // 手势缩放 ImageView：https://github.com/Baseflow/PhotoView
    // 隐私调用监控：https://github.com/huage2580/PermissionMonitor
    // 动画解析库：https://github.com/airbnb/lottie-android
    // 动画资源：https://lottiefiles.com、https://icons8.com/animated-icons

}
